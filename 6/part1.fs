\ Day 6

[DEFINED] EMPTY [IF] EMPTY [THEN] MARKER EMPTY
include ../ttester.fs
CLEARSTACK DECIMAL

VARIABLE IN
10000 CONSTANT MAX-LINE
: ?ERR   ABORT" File I/O error" ;
: OPEN   ( a n -- )  R/O OPEN-FILE ?ERR  IN ! ;
: CLOSE  IN @ CLOSE-FILE ?ERR ;
: READ   ( a -- n flag )  MAX-LINE IN @ READ-LINE ?ERR ;

VARIABLE ROWS
VARIABLE COLS
: NUMBER ( str -- n )  0 0 ROT COUNT >NUMBER 2DROP DROP ;
: %   0  BEGIN  BL WORD  DUP C@ WHILE  NUMBER ,  1+  REPEAT DROP  COLS !
      1 ROWS +! ;

: PROCESS ( a n ) 
    OVER C@  DUP '*' = SWAP '+' = OR  0= IF  -2 /STRING  THEN  EVALUATE ;

CREATE LINE   '%' C, BL C,  MAX-LINE ALLOT

0 VALUE DATA
: INPUT  ( a n -- )  ALIGN  HERE TO DATA  ROWS OFF  COLS OFF  OPEN
    BEGIN  LINE 2 + DUP READ WHILE  PROCESS
    REPEAT 2DROP CLOSE ;

: AT ( row col -- n )  SWAP COLS @ * +  CELLS DATA + @ ;
VARIABLE TOTAL
VARIABLE COL
: +   0  ROWS @ 0 DO  I COL @ AT +  LOOP  TOTAL +!  1 COL +! ;
: *   1  ROWS @ 0 DO  I COL @ AT *  LOOP  TOTAL +!  1 COL +! ;

S" input.txt" INPUT

T{ TOTAL @ -> 5335495999141 }T
